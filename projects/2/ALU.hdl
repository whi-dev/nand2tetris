// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // if zx == 1 then x = 0
    Mux16(a=x, b=false, sel=zx, out=x0);
    // if nx == 1 then x = !x
    Not16(in=x0, out=notx);
    Mux16(a=x0, b=notx, sel=nx, out=x1);
    // if zy == 1 then y = 0
    Mux16(a=y, b=false, sel=zy, out=y0);
    // if ny == 1 then y = !y
    Not16(in=y0, out=noty);
    Mux16(a=y0, b=noty, sel=ny, out=y1);
    // if f == 1 then out = x + y
    // if f == 0 then out = x & y
    Add16(a=x1, b=y1, out=add);
    And16(a=x1, b=y1, out=and);
    Mux16(a=and, b=add, sel=f, out=res1);
    // if no == 1 then out = !out
    Not16(in=res1, out=notres1);
    Mux16(a=res1, b=notres1, sel=no, out=out, out[0..7]=out0, out[8..15]=out1, out[15]=ng);

    Or8Way(in=out0, out=zr0);
    Or8Way(in=out1, out=zr1);
    Or(a=zr0, b=zr1, out=zr2);
    Not(in=zr2, out=zr);
}